// FINS grammar
?start: command_chain | function_definition

command_chain: command ("->" command)*

?command: sort_command
        | filter_command
        | basket
        | variable
        | set_operation
        | column_command
        | info_command
        | lock_command
        | unlock_command

// Basic commands
sort_command: "sort" ATTRIBUTE ORDER
filter_command: ATTRIBUTE COMPARISON_OPERATOR NUMBER

// Basket creation
basket: symbol+

// Variables and symbols
variable: VARIABLE
symbol: STOCK_SYMBOL | ETF_SYMBOL | INDEX_SYMBOL

// Set operations
set_operation: union_op | difference_op | intersection_op | spread_op
union_op: "+" (basket | variable)
difference_op: "-" (basket | variable)
intersection_op: "&" (basket | variable)
spread_op: ".." symbol

// Column addition
column_command: [column_name] column_type [time_range] [period]
column_name: "|" NAME
column_type: "cagr" | "dividend" | "pe" | NAME
time_range: "[" YEAR ":" YEAR "]"
period: /[0-9]+[ymd]/

// Info command
info_command: ":" (symbol | variable)

// Function definition
function_definition: "DEFINE" FUNCTION_VARIABLE "=" STRING

// Terminal symbols
STOCK_SYMBOL: /[A-Z][A-Z0-9]*/
ETF_SYMBOL: /[A-Z][A-Z0-9]*/
INDEX_SYMBOL: /\^[A-Z][A-Z0-9]*/
VARIABLE: /[$\/][A-Za-z0-9_\/]+/
FUNCTION_VARIABLE: /![A-Za-z0-9_]+/
NAME: /[a-zA-Z][a-zA-Z0-9_]*/
ATTRIBUTE: /[a-zA-Z_][a-zA-Z0-9_]*/
ORDER: "asc" | "desc"
COMPARISON_OPERATOR: ">" | "<" | "="
NUMBER: /[0-9]+([KMBTkmbt])?/
YEAR: /[0-9]{4}/
STRING: /"[^"]*"/

// Variable management
lock_command: "lock" VARIABLE
unlock_command: "unlock" VARIABLE

%import common.WS
%ignore WS
