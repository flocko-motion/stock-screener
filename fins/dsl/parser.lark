// FINS grammar - main rules
?start: command_chain | function_definition | help_command | management_command | note_command

// Command chains (the core flow syntax)
command_chain: command ("->" command)*
?command: function_call | sequence | column_function

// Functions and arguments
function_call: [assignment] NAME "(" [arg_list] ")"
column_function: [column_assignment] "." NAME "(" [arg_list] ")"
function_definition: "function" NAME "(" [arg_list] ")" "{" command_chain "}"
management_command: NAME "(" [arg_list] ")"

// Assignments
assignment: NAME "="
column_assignment: "." NAME "="

// Arguments
arg_list: named_arg ("," named_arg)*
named_arg: NAME "=" value
value: NUMBER | STRING | NAME | column_ref | list
column_ref: "." NAME
list: "[" value ("," value)* "]"

// Basket operations
?sequence: (OPERATOR? operand+)+
?operand: WEIGHT? (symbol | variable)
OPERATOR: "+" | "-" | "&"

// Help system
help_command: "?" [help_topic]
help_topic: NAME | "." NAME | "." | "*"

// Notes system
note_command: "@" [NAME] "(" [arg_list] ")" [NOTE_TEXT]
NOTE_TEXT: /[^\n]+(\n[^@][^\n]*)*/

// Variables and symbols
variable: TEMP_BASKET | SAVED_BASKET
TEMP_BASKET: /\$[a-zA-Z0-9_\/]+/
SAVED_BASKET: /\/[a-zA-Z0-9_\/]+/
symbol: SYMBOL
SYMBOL: /\^?[A-Z][A-Z0-9]*/
WEIGHT: NUMBER "x"? | NUMBER

// Basic types
NUMBER: /[0-9]+(\.[0-9]+)?[KMBT%]?/
STRING: /"[^"]*"/
NAME: /[a-zA-Z_][a-zA-Z0-9_]*/

// Ignore whitespace and comments
%ignore /[ \t\f\r\n]+/
%ignore /\/\/[^\n]*/
