// FINS grammar - main rules
?start: command_chain | function_definition | function_help | management_command | note_command

// Command chains (the core flow syntax)
command_chain: command ("->" command)*
?command: sequence | function_call | column_function

// Functions and arguments
function_call: NAME "(" [arg_list_positional_and_named] ")"
column_function: [column_assignment] "." NAME "(" [arg_list_named] ")"
function_definition: "function" NAME "(" [arg_list_named] ")" "{" command_chain "}"
management_command: NAME "(" [arg_list_named] ")"

// Assignments
column_assignment: "." NAME "="

// Arguments
arg_list_positional_and_named: (unnamed_arg ",")* unnamed_arg | named_arg ("," named_arg)*
arg_list_named: named_arg ("," named_arg)*
unnamed_arg: NAME | column_ref
named_arg: NAME "=" value
value: NUMBER | STRING | NAME | column_ref | list
column_ref: "." NAME
list: "[" value ("," value)* "]"

// Basket operations
?sequence: (OPERATOR? operand+)+
?operand: WEIGHT? (symbol | variable)
OPERATOR: "+" | "-" | "&"

// Help system
function_help: "?" [help_topic]
help_topic: NAME | "." NAME | "." | "*"

// Notes system
note_command: "@" [NAME] "(" [arg_list_named] ")" [NOTE_TEXT]
NOTE_TEXT: /[^\n]+(\n[^@][^\n]*)*/

// Variables and symbols
variable: TEMP_BASKET | SAVED_BASKET
TEMP_BASKET: /\$[a-zA-Z0-9_\/]+/
SAVED_BASKET: /\/[a-zA-Z0-9_\/]+/
symbol: SYMBOL
SYMBOL: /\^?[A-Z][A-Z0-9]*/
WEIGHT: NUMBER "x"? | NUMBER

// Basic types
NUMBER: /[0-9]+(\.[0-9]+)?[KMBT%]?/
STRING: /"[^"]*"/
NAME: /[a-zA-Z_][a-zA-Z0-9_]*/

// Ignore whitespace and comments
%ignore /[ \t\f\r\n]+/
%ignore /\/\/[^\n]*/
